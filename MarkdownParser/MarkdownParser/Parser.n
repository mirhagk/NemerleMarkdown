using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Peg;

using System;
using System.Collections.Generic;
using System.Linq;

namespace MarkdownParser
{
  /// <summary>
  /// Description of Parser.
  /// </summary>
    [PegGrammar(Options = EmitDebugSources, document,
    grammar
    {
        any : char = ['\u0000'..'\uFFFF'];
        anyChar : char = !('\n' / '\r') any;
        number = ['0' .. '9'];
        content : ContentNode = bold / italic / plain;
        plain : ContentNode = anyChar;
        bold : ContentNode = "**" (!'*' plain / italic)+ "**";
        italic: ContentNode = '*' (!'*' plain / bold) + '*';
        newline : void = linebreak;
        newlines : void = newline+;
        spaces : void = ' '*;
        horizontalRule : Node = '-' spaces '-' spaces '-' (spaces '-')* / '=' spaces '=' spaces '=' (spaces '=')* / '*' spaces '*' spaces '*' (spaces '*')*;
        setextBottom : char = '=' spaces '=' spaces '=' (spaces '=')* / '-' spaces '-' spaces '-' (spaces '-')*;
        setextHeading : Node = content* newline setextBottom newline;
        headingMatchSymbol : char = '#';
        atxHeading : Node = headingMatchSymbol+ spaces (!('#') content)* '#'* newlines?;
        heading : Node = atxHeading / setextHeading;
        docPiece : Node = heading;
        paragraph: Node = (!docPiece content+ newline?)+;
        //paragraph: Node = (content+ newline?)+;
        linebreak : Node = ("\n\r" / "\r\n" / '\r' / 'n');
        document : List[Node] = (docPiece / paragraph / linebreak)*;
        //document : List[Node] = (paragraph / linebreak)*;
      //Rules
    }
  )]
  public class Parser
  {
      private listCharToString(text: List[char]):string
      {
          string.Join("",text);
      }
      public mutable filename : string;
      content(x: ContentNode):ContentNode
      {
          x;
      }
      plain(x : char):ContentNode
      {
          ContentNode(ContentNode.NodeType.Text,List(),x.ToString());
      }
      bold(_ : NToken, nodes :List[ContentNode], _:NToken):ContentNode
      {
          ContentNode(ContentNode.NodeType.Bold,nodes,"");
      }
      italic(_:NToken, nodes: List[ContentNode], _:NToken):ContentNode
      {
          ContentNode(ContentNode.NodeType.Italics,nodes,"");
      }
      horizontalRule(_ : NToken, _:NToken,_:NToken, _:List[NToken]):Node
      {
          Node(Node.NodeType.HorizontalRule(),List());
      }
      headingMatchSymbol(x : NToken) : char
      {
          GetText(x)[0];
      }
      setextBottom(symbol: NToken, _ : NToken, _: NToken, _: List[NToken]):char
      {
          GetText(symbol)[0];
      }
      setextHeading(text: List[ContentNode], symbol :char) : Node
      {
          createHeader(if (symbol == '=') 1 else 2, text);
      }
      createHeader(level: int, text: List[ContentNode]) : Node
      {
          Node(Node.NodeType.Heading(level),text);
      }
      atxHeading(lvl : List[char], text: List[ContentNode], _ : NToken):Node
      {
          createHeader(lvl.Count(),text);
      }
      linebreak(_ : NToken):Node
      {
          Node(Node.NodeType.LineBreak(),List());
      }
      any(letter: NToken):char
      {
          GetText(letter)[0];
      }
      paragraph( text: List[List[ContentNode]]):Node
      {
          //def text = string.Join("",letters.Select((x)=>string.Join("",x)));
          Node(Node.NodeType.Paragraph(),text.Flatten().ToList());
      }
      document (pieces:List[Node]):List[Node]
      {
          pieces;
      }
  }
}
